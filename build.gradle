plugins {
    id "java-library"
    id "net.minecraftforge.gradle" version "5.1.+"
}

version = modVersion
group = modGroup
archivesBaseName = modBaseName

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

minecraft {
    mappings channel: project.mcpChannel, version: project.mcpVersion
    accessTransformers(
        project.file("jei_at.cfg"), // forgegradle is too dumb to include the AT from the jei dep
        project.file("src/main/resources/META-INF/betterquesting_at.cfg")
    )

    runs {
        client {
            workingDirectory project.file("run")

            mods {
                betterquesting {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
    // JEI
    maven { url = "https://dvs1.progwml6.com/files/maven/" }
}

dependencies {
    minecraft "net.minecraftforge:forge:$project.forgeVersion"
    implementation fg.deobf("mezz.jei:jei_1.12.2:4.16.1.302")
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcVersion", project.mcVersion

    from(sourceSets.main.resources.srcDirs) {
        include "*.in"
        rename "(.*)\\.in", "\$1"

        expand "version": project.version, "mcVersion": project.mcVersion
    }
}

jar {
    manifest {
        attributes "FMLAT": "betterquesting_at.cfg"
    }

    finalizedBy "reobfJar"
}

tasks.register("deobfJar", Jar) {
    from sourceSets.main.output
    archiveAppendix = "deobf"
}

tasks.register("apiJar", Jar) {
    from(sourceSets.main.output) {
        include "betterquesting/api/**"
        include "betterquesting/api2/**"
    }
    archiveAppendix = "api"
}
 
artifacts {
    archives deobfJar
    archives apiJar
}
